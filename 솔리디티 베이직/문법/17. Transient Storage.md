# Transient Storage

## 목표
- Solidity에서 Transient Storage의 개념과 사용법을 학습한다.
- Transient Storage를 활용하여 스마트 계약의 가스 비용을 최적화하는 방법을 이해한다.
- EIP-1153에 따른 TSTORE와 TLOAD 명령어의 동작을 이해하고, 이를 사용한 코드 작성 방법을 습득한다.
- 제공된 예제 코드를 Remix IDE에서 작성하고 실행하여 Transient Storage의 동작을 확인한다.
- Transient Storage를 활용한 재진입 잠금(reentrancy locks)의 구현 방법과 그 효과를 비교 분석한다.


#### Transient Stroage란
Solidity 0.8.24는 최근 Cancun 하드포크에 포함된 `TSTORE`와 `TLOAD` 등의 명령어를 지원한다. 이 명령어들은 EIP-1153에 따라 일시적 저장소(Transient Storage)를 다룹니다. 일시적 저장소는 메모리, 스토리지, calldata 외에 또 다른 데이터 저장 위치를 제공하며, 주된 차이점은 데이터가 영구적이지 않고 현재 트랜잭션에 한정된다는 것이다. 따라서 트랜잭션 후에는 데이터가 0으로 리셋된다. 일시적 저장소 접근 비용은 100 가스로 저렴한다.

- `TSTORE` (저장): 일시적 저장소에 데이터를 저장하는 작업이다. 데이터를 특정 키에 저장하며, 이 과정에서 100 가스의 비용이 발생한다.
- `TLOAD` (불러오기): 일시적 저장소에 저장된 데이터를 특정 키를 통해 불러오는 작업이다. 이 작업 역시 100 가스의 비용이 발생한다.

(일시적 저장소에 저장된 데이터를 불러오는 작업(TLOAD)은 EVM이 데이터를 관리하고 접근하는 데 필요한 자원을 사용한다. 이 작업은 단순한 조회(view 등)보다 복잡하며, 일시적 저장소의 상태 관리를 포함한다.)

그러나 현재 컴파일러는 고급 Solidity 코드에서 일시적 저장소를 데이터 위치로 사용하는 것을 허용하지 않는다. 현재로서는 inline 어셈블리에서 `TSTORE`와 `TLOAD` 명령어를 사용해 접근할 수 있다.

일시적 저장소의 주요 사용 사례는 저렴한 재진입 잠금(reentrancy locks)이 있다.


#### Transient Storage의 특성 
- 임시 저장: Transient Storage는 트랜잭션 동안만 존재하며, 트랜잭션이 끝나면 데이터가 초기화된다. 즉, 트랜잭션 범위 내에서만 유효하다.
- 저장 비용 없음: 데이터가 블록체인에 영구적으로 저장되지 않으므로, 장기적인 저장 비용은 발생하지 않지만, 접근 및 저장하는 데 가스 비용이 발생한다. 구체적으로, `TSTORE`와 `TLOAD` 명령어를 사용할 때 각각 100 가스의 비용이 부과된다.
- 빠른 접근 속도: Transient Storage는 영구 저장소인 'storage'에 비해 상대적으로 접근 속도가 빠르며, 비용도 저렴하다.


## 예제 코드
```solidity
pragma solidity ^0.8.24;

// Make sure EVM version and VM set to Cancun

// Storage - data is stored on the blockchain
// Memory - data is cleared out after a function call
// Transient storage - data is cleared out after a transaction

interface ITest {
    function val() external view returns (uint256);
    function test() external;
}

contract Callback {
    uint256 public val;

    fallback() external {
        val = ITest(msg.sender).val();
    }

    function test(address target) external {
        ITest(target).test();
    }
}

contract TestStorage {
    uint256 public val;

    function test() public {
        val = 123;
        bytes memory b = "";
        msg.sender.call(b);
    }
}

contract TestTransientStorage {
    bytes32 constant SLOT = 0;

    function test() public {
        assembly {
            tstore(SLOT, 321)
        }
        bytes memory b = "";
        msg.sender.call(b);
    }

    function val() public view returns (uint256 v) {
        assembly {
            v := tload(SLOT)
        }
    }
}

contract ReentrancyGuard {
    bool private locked;

    modifier lock() {
        require(!locked);
        locked = true;
        _;
        locked = false;
    }

    // 35313 gas
    function test() public lock {
        // Ignore call error
        bytes memory b = "";
        msg.sender.call(b);
    }
}

contract ReentrancyGuardTransient {
    bytes32 constant SLOT = 0;

    modifier lock() {
        assembly {
            if tload(SLOT) { revert(0, 0) }
            tstore(SLOT, 1)
        }
        _;
        assembly {
            tstore(SLOT, 0)
        }
    }

    // 21887 gas
    function test() external lock {
        // Ignore call error
        bytes memory b = "";
        msg.sender.call(b);
    }
}

```
Transient Storage는 어려운 내용이기 때문에 코드 설명 대신 바로 실습으로 transient storage의 특징을 확인해보자.

## Remix에서 실습 
1. Remix에서 새로운 solidity 파일 생성해서 예제 코드를 복사 붙여넣기 한다.
2. 예제 코드를 compile 후 deploy한다. 4개의 컨트랙트(Callback, TestStorage, TestTransientStorage, ReentrancyGuard, ReentrancyGuardTransient)
3. 아래 버튼들이 제대로 동작하는지 확인한다.

- Callback 컨트랙트의 test 함수를 호출한다. 인자로 TestStorage를 먼저 입력한다. 

<img src= "https://github.com/Joon2000/Solidity-modules/blob/main/images/TransientStorage/TestStorage.png" width="300px" height="500px" 
  title="getStateVariable" alt="getStateVariable"> <br/>
`val`버튼을 눌러보면 Callback과 TestStorage 모두 `123`이 나오는 것을 확인할 수 있다. 

이번에는 TestTransientStorage 주소를 인자로 입력하고 Callback의 test 함수를 호출한다. <br>
<img src= "https://github.com/Joon2000/Solidity-modules/blob/main/images/TransientStorage/TestTransientStorage.png" width="300px" height="600px" 
  title="getStateVariable" alt="getStateVariable"><br/>
`val`버튼을 눌러보면 Callback은 `321`이 나오지만 TestTransientStorage는 `0`이 나오는 것을 확인할 수 있다. TestTransientStorage 컨트랙트의 `val` 함수 호출 시 `0`이 나오는 이유는 `_transientVal` 변수가 트랜잭션이 끝나면 `0`으로 재설정되기 때문이다. <br>

- ReentrancyGuard와 ReentrancyGuardTransient의 `test`함수를 실행해보면 ReentrancyGuardTransient의 가스비가 ReentrancyGuard의 가스비보다 더 적게 든 것을 확인할 수 있다.

ReentrancyGuard의 가스비 <br>
<img src= "https://github.com/Joon2000/Solidity-modules/blob/main/images/TransientStorage/ReentryGuardGas.png" width="1000px" height="400px" 
  title="getStateVariable" alt="getStateVariable"><br/>
ReentrancyGuardTransient의 가스비 <br>
<img src= "https://github.com/Joon2000/Solidity-modules/blob/main/images/TransientStorage/ReentryGuardTransientGas.png" width="1000px" height="400px" 
  title="getStateVariable" alt="getStateVariable"><br/>
